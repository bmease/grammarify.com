$ = require 'jquery'
_ = require 'lodash'
nlp = require 'nlp_compromise'
utilities = require 'utilities'
hasPunctuation = utilities.hasPunctuation
stripPunctuation = utilities.stripPunctuation
Backbone = require 'backbone'
Backbone.$ = $

Token = require 'models/token'


module.exports = class TokenCollection extends Backbone.Collection
    model: Token

    query: (q) ->
        sentences = nlp.pos(q, {dont_combine: true}).sentences

        tokens = []
        for sentence in sentences
            for t in sentence.tokens
                # Don't add fake tokens generated by nlp
                if t.text

                    # Generate tokens for punctuation to make it look better
                    punctuation = hasPunctuation t.text
                    if punctuation then tokens.push punctuation

                    tokens.push new Token {
                        pos: t.pos.parent
                        token: stripPunctuation t.text
                        name: t.pos.name
                        example: t.pos.example
                        reason: t.pos_reason
                    }

                    punctuation = hasPunctuation t.text, true
                    if punctuation then tokens.push punctuation

        @reset tokens
